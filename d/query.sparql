PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX aqr: <http://reference.eionet.europa.eu/aq/ontology/>
PREFIX rod-schema: <http://rod.eionet.europa.eu/schema.rdf#>

SELECT DISTINCT 
?spoURI as ?_id
?CountryOrTerritory 
YEAR(?startOfPeriod) as ?ReportingYear 
?Namespace 
?Network 
?Timezone 
?ResponsibleParty 
?StationName 
?EUStationCode as ?EoICode 
?StationType 
?StationArea 
?Pollutant 
?SamplingPointId 
?MeasurementRegime 
?OperationalActivityBegin 
?OperationalActivityEnd 
?Sample 
?SamplingProcess 
?MeasurementEquipment 
?MeasurementMethod 
?SamplingEquipment
?AnalyticalTechnique 
?envelope

# bif:either(?usedAQD > 0, "YES","NO") AS ?UsedForAQD ?ZoneCode ?ZoneName ?ZoneResidentPopulation


 WHERE {

  ?spoURI a aqr:SamplingPoint;
            dcterms:source ?source;
            aqr:inspireId ?SamplingPointId;
            aqr:inspireNamespace ?Namespace;
            aqr:observingCapability ?ocURI ;
            aqr:broader ?staURI ;
#            aqr:usedAQD ?usedAQD ;
            aqr:belongsTo ?netURI .

?ocURI aqr:observedProperty ?pollURI .
?ocURI aqr:featureOfInterest ?samURI .
?ocURI aqr:procedure ?sppURI .

 ?envelope rod-schema:hasFile ?source .
 ?envelope rod-schema:locality ?locURI .
 ?envelope rod-schema:startOfPeriod ?startOfPeriod .
 ?locURI rdfs:label ?CountryOrTerritory .

OPTIONAL{?staURI aqr:EUStationCode ?EUStationCode} .
OPTIONAL{?staURI aqr:areaClassification ?areaURI} .
OPTIONAL{?staURI rdfs:label ?StationName} .

OPTIONAL{?spoURI aqr:relevantEmissions ?relemiURI} .
OPTIONAL{?spoURI aqr:operationalActivityBegin ?OperationalActivityBegin} . 
OPTIONAL{?spoURI aqr:operationalActivityEnd ?OperationalActivityEnd} .
OPTIONAL{?spoURI aqr:measurementRegime ?measurementRegimeURI} .
OPTIONAL{?measurementRegimeURI rdfs:label ?MeasurementRegime} .
#OPTIONAL{?spoURI aqr:zone ?zonURI} .

#OPTIONAL{?zonURI aqr:zoneCode ?ZoneCode} .
#OPTIONAL{?zonURI aqr:residentPopulation ?ZoneResidentPopulation} .
#OPTIONAL{?zonURI rdfs:label ?ZoneName} .

OPTIONAL{?pollURI skos:notation ?Pollutant} .
OPTIONAL{?samURI aqr:inspireId ?Sample} .
OPTIONAL{?sppURI aqr:inspireId ?SamplingProcess} .
OPTIONAL{?netURI aqr:inspireId ?Network} .
OPTIONAL{?netURI aqr:aggregationTimeZone ?TimezoneURI} .
OPTIONAL{?TimezoneURI rdfs:label ?Timezone} .
OPTIONAL{?netURI aqr:responsibleParty ?ResponsibleParty} .

OPTIONAL{?sppURI aqr:measurementEquipment ?equiURI} .
OPTIONAL{?sppURI aqr:measurementMethod ?metURI} .
OPTIONAL{?sppURI aqr:analyticalTechnique ?analiticURI} .
OPTIONAL{?sppURI aqr:samplingEquipment ?samequiURI} .

OPTIONAL{?areaURI rdfs:label ?StationArea} .
OPTIONAL{?relemiURI aqr:stationClassification ?staclassURI} .
OPTIONAL{?staclassURI rdfs:label ?StationType} .
OPTIONAL{?equiURI rdfs:label ?MeasurementEquipment} .
OPTIONAL{?samequiURI aq:equipment ?samequiURI2} .
OPTIONAL{?samequiURI2 rdfs:label ?SamplingEquipment} .
OPTIONAL{?metURI rdfs:label ?MeasurementMethod} .
OPTIONAL{?analiticURI rdfs:label ?AnalyticalTechnique} .

#FILTER regex(?Namespace,"AT.0008.20.AQ") .
#FILTER regex(?pollURI,"") .
            
} ORDER BY ?Namespace ?pollURI ?EUStationCode