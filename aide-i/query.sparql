PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX aqr: <http://reference.eionet.europa.eu/aq/ontology/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX aqdd: <http://dd.eionet.europa.eu/property/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 

SELECT 
#?sourceappXMLURI
bif:concat(xsd:string(YEAR(?endOfPeriod)), '-', ?SourceApportionmentId ) as ?_id
?Country as ?CountryOrTerritory 
YEAR(xsd:date(?endOfPeriod)) AS ?ReportingYear 
?Namespace 
?SourceApportionmentId 
?VersionId 
?ReasonOfExceedance 
?NumericalExceedance 
?NumberOfExceedances 
?TotalIncrementQuantity 
?TotalIncrementComment 
?TotalRegionalQuantity 
?TotalRegionalComment 
?TotalUrbanQuantity 
?TotalUrbanComment 
?Comment 
?AttainmentId 
?PlanId 
?envelope

 WHERE {

{SELECT 
?sourceappXMLURI 
(IRI(substr(str(?sourceappXMLURI),1,49)) AS ?envelope) 

WHERE {
?sourceappXMLURI a aq:AQD_SourceApportionment .
}
}

?envelope rod:released ?released .

{
SELECT DISTINCT 
?Country 
?obligation 
(YEAR(xsd:date(?startOfPeriod)) AS ?ReportingYear) 
(max(?released) AS ?released) 

WHERE {
?envelope rod:released ?released ;
          rod:startOfPeriod ?startOfPeriod ;
          rod:obligation <http://rod.eionet.europa.eu/obligations/681> ; 
          rod:obligation ?obligation ;
          rod:locality ?locality .

?locality rod:localityName ?Country .

}
}

   ?sourceappXMLURI a aq:AQD_SourceApportionment ;
                      aq:parentExceedanceSituation ?AttainmentId ;
                      aq:macroExceedanceSituation ?macroURI ;
                      aq:usedInPlan ?PlanId ;
                      aq:localIncrement ?incrementURI ;
                      aq:regionalBackground ?regURI ;
                      aq:urbanBackground ?urbanURI ; 
                      aq:inspireId ?inspireURI . 

OPTIONAL {?sourceappXMLURI aq:comment ?Comment} .

?incrementURI aq:total ?totalincrementURI .
?regURI aq:total ?totalregURI .
?urbanURI aq:total ?totalurbanURI .

OPTIONAL {?totalincrementURI aq:comment ?TotalIncrementComment} .
OPTIONAL {?totalincrementURI aq:quantity ?TotalIncrementQuantity} . 

OPTIONAL {?totalregURI aq:comment ?TotalRegionalComment} .
OPTIONAL {?totalregURI aq:quantity ?TotalRegionalQuantity} . 

OPTIONAL {?totalurbanURI aq:comment ?TotalUrbanComment} .
OPTIONAL {?totalurbanURI aq:quantity ?TotalUrbanQuantity} . 

OPTIONAL {?macroURI aq:reason ?reasonURI} . 
OPTIONAL {?reasonURI rdfs:label ?ReasonOfExceedance} .
OPTIONAL {?macroURI aq:numericalExceedance ?NumericalExceedance} . 
OPTIONAL {?macroURI aq:numberExceedances ?NumberOfExceedances} . 

?inspireURI aq:namespace ?Namespace ;
            aq:localId ?SourceApportionmentId ;
            aq:versionId ?VersionId .

?envelope rod:locality ?locality ;
          rod:obligation ?obliURI ;
          rod:endOfPeriod ?endOfPeriod .

?locality rod:localityName ?Country .
?obliURI rdfs:label ?Obligation .


   
}