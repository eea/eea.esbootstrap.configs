PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#>
PREFIX obligations: <http://rod.eionet.europa.eu/obligations/>
PREFIX aqdd: <http://dd.eionet.europa.eu/property/>

SELECT DISTINCT

md5(concat(?Country, xsd:string(?ReportingYear),xsd:string(?referenceYear),?Namespace ,?SourceApportionmentId ,?VersionId ,?ReasonOfExceedance, xsd:string(?NumericalExceedance) ,xsd:string(?NumberOfExceedances) ,xsd:string(?TotalIncrementQuantity),?TotalIncrementComment ,xsd:string(?TotalRegionalQuantity) ,?TotalRegionalComment,xsd:string(?TotalUrbanQuantity) ,?TotalUrbanComment ,?Comment ,?AttainmentId ,?PlanId ,?envelope))  as ?_id

#?XMLURI
#bif:concat(xsd:string(YEAR(?endOfPeriod)), '-', ?SourceApportionmentId ) as ?_id
?Country as ?CountryOrTerritory 
YEAR(xsd:date(?endOfPeriod)) AS ?ReportingYear
?referenceYear
?Namespace 
?SourceApportionmentId 
?VersionId 
?ReasonOfExceedance 
?NumericalExceedance 
?NumberOfExceedances 
?TotalIncrementQuantity 
?TotalIncrementComment 
?TotalRegionalQuantity 
?TotalRegionalComment 
?TotalUrbanQuantity 
?TotalUrbanComment 
?Comment 
replace(replace(replace(str(?AttainmentId),"http://reference.eionet.europa.eu/aq/",""),?Namespace ,""),"/","") as ?AttainmentId 
replace(replace(replace(str(?PlanId),"http://reference.eionet.europa.eu/aq/",""),?Namespace ,""),"/","") as ?PlanId  
?envelope

WHERE {{
  GRAPH ?source {
    ?XMLURI a aq:AQD_SourceApportionment .
    ?XMLURI aq:macroExceedanceSituation ?macroURI .
    ?XMLURI aq:usedInPlan ?PlanId .
    ?XMLURI aq:inspireId ?inspireURI .
  }
  ?source dcterms:isPartOf ?envelope .
  ?envelope rod:released ?released .
  ?envelope rod:locality ?locality .
  ?envelope rod:obligation ?obliURI .
  ?envelope rod:startOfPeriod ?startOfPeriod .
  ?envelope rod:endOfPeriod ?endOfPeriod .

  OPTIONAL {?XMLURI aq:localIncrement ?incrementURI} .
  OPTIONAL {?XMLURI aq:urbanBackground ?urbanURI} .
  OPTIONAL {?XMLURI aq:regionalBackground ?regURI} .
  OPTIONAL {?XMLURI aq:parentExceedanceSituation ?AttainmentId} .

  OPTIONAL {?XMLURI aq:comment ?Comment} .
  OPTIONAL {?XMLURI aq:referenceYear ?referenceYearURI} .
  OPTIONAL {?referenceYearURI rdfs:label ?referenceYear} .

  OPTIONAL {?incrementURI aq:total ?totalincrementURI} .
  OPTIONAL {?regURI aq:total ?totalregURI} .
  OPTIONAL {?urbanURI aq:total ?totalurbanURI} .

  OPTIONAL {?totalincrementURI aq:comment ?TotalIncrementComment} .
  OPTIONAL {?totalincrementURI aq:quantity ?TotalIncrementQuantity} . 

  OPTIONAL {?totalregURI aq:comment ?TotalRegionalComment} .
  OPTIONAL {?totalregURI aq:quantity ?TotalRegionalQuantity} . 

  OPTIONAL {?totalurbanURI aq:comment ?TotalUrbanComment} .
  OPTIONAL {?totalurbanURI aq:quantity ?TotalUrbanQuantity} . 

  OPTIONAL {?macroURI aq:reason ?reasonURI} . 
  OPTIONAL {?reasonURI rdfs:label ?ReasonOfExceedance} .
  OPTIONAL {?macroURI aq:numericalExceedance ?NumericalExceedance} . 
  OPTIONAL {?macroURI aq:numberExceedances ?NumberOfExceedances} . 

  ?inspireURI aq:namespace ?Namespace .
  ?inspireURI aq:localId ?SourceApportionmentId .
  ?inspireURI  aq:versionId ?VersionId .

  ?locality rod:localityName ?Country .
  ?obliURI rdfs:label ?Obligation .

  #FILTER (?envelope = <http://cdr.eionet.europa.eu/bg/eu/aqd/i/envvnkgpw>)
  FILTER (?envelope = <<envelope>>)
}}