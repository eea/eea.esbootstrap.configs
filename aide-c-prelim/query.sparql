PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#>
PREFIX obligations: <http://rod.eionet.europa.eu/obligations/>

SELECT DISTINCT

md5(concat(?Country, ?AssessmentId, ?Namespace, ?ZoneURI, xsd:string(?startOfPeriod), ?ObjectiveType, ?ReportingMetric, ?ExceedanceAttainment, xsd:string(?ClassificationDate), ?ClassificationReport, ?AssessmentType, ?Pollutant, ?ProtectionTarget, ?Description, ?samplingPointURI, ?ModelAssessmentMetadataURI, ?envelope))  as ?_id

?Country as ?CountryOrTerritory
?AssessmentId
?Namespace
?Pollutant

replace(replace(replace(str(?ZoneURI),"http://reference.eionet.europa.eu/aq/",""),?Namespace,""),"/","") as
?ZoneId

YEAR(xsd:date(?startOfPeriod)) AS ?ReportingYear
?ObjectiveType
?ReportingMetric
?ProtectionTarget
?ExceedanceAttainment
?ClassificationDate
?ClassificationReport
?AssessmentType
?Description

replace (replace(replace(str(?samplingPointURI),"http://reference.eionet.europa.eu/aq/",""),?Namespace,""),"/","") as ?SamplingPointId

replace (replace(replace(str(?ModelAssessmentMetadataURI),"http://reference.eionet.europa.eu/aq/",""),?Namespace,""),"/","") as ?ModelId
?envelope

WHERE {{
  #test values
  #values ?envelope { <http://cdr.eionet.europa.eu/at/eu/aqd/c_preliminary/envwcwtba> }
  #values ?Country { 'Austria' }
  #values ?Obligation { '[Air Quality] (C) Preliminary information on the assessment regime (Article 7)' }
  #values ?released { '2016-12-22T13:20:11Z' }
  #values ?startOfPeriod { '2017-01-01' }
  #GRAPH <http://cdr.eionet.europa.eu/at/eu/aqd/c_preliminary/envwcwtba/AQD_Dataset_C_2017_AT.xml> {
  
  values ?envelope { <<envelope>> }
  values ?Country { '<Country>' }
  values ?Obligation { '<Obligation>' }
  values ?released { '<released>' }
  values ?startOfPeriod { '<startOfPeriod>' }

  GRAPH <<source>> {
    ?XMLURI a aq:AQD_AssessmentRegime .
    ?XMLURI aq:assessmentMethods ?assesmentMethodsURI .
    ?XMLURI aq:assessmentThreshold ?assesmentThresoldURI .
    ?XMLURI aq:inspireId ?inspireURI .
    ?XMLURI aq:pollutant ?polltargetURI .
    ?XMLURI aq:declarationFor ?declaration .
    ?XMLURI aq:zone ?ZoneURI .
    ?assesmentThresoldURI aq:environmentalObjective ?environmentalObjective .  
    ?assesmentThresoldURI aq:exceedanceAttainment ?ExceedanceAttainmentURI .
    ?assesmentThresoldURI aq:classificationDate ?classdateURI .
    ?assesmentMethodsURI aq:assessmentType ?AssessmentTypeURI .
    ?environmentalObjective aq:objectiveType ?ObjectiveTypeURI . 
    ?environmentalObjective aq:reportingMetric ?ReportingMetricURI .
    ?environmentalObjective aq:protectionTarget ?ProtectionTargetURI .
    ?inspireURI aq:localId ?AssessmentId .
    ?inspireURI aq:namespace ?Namespace.
    ?classdateURI rdfs:label ?ClassificationDate .
    OPTIONAL{?assesmentThresoldURI aq:classificationReport ?ClassificationReport} .
    OPTIONAL{?assesmentMethodsURI aq:assessmentTypeDescription ?Description} .
    OPTIONAL{?assesmentMethodsURI aq:samplingPointAssessmentMetadata ?samplingPointURI} .
    OPTIONAL{?assesmentThresoldURI aq:classificationReport ?ClassificationReport} .
    OPTIONAL{?assesmentMethodsURI aq:modelAssessmentMetadata ?ModelAssessmentMetadataURI} .
  }

  GRAPH <http://dd.eionet.europa.eu/vocabularyfolder/aq/rdf> { 
    ?ObjectiveTypeURI rdfs:label ?ObjectiveType .
    ?polltargetURI skos:notation ?Pollutant .
    ?ReportingMetricURI rdfs:label ?ReportingMetric .
    ?ProtectionTargetURI rdfs:label ?ProtectionTarget .
    ?ExceedanceAttainmentURI skos:notation ?ExceedanceAttainment .
    ?AssessmentTypeURI rdfs:label ?AssessmentType .
  }

}}
