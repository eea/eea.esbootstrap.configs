PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX aqr: <http://reference.eionet.europa.eu/aq/ontology/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX aqdd: <http://dd.eionet.europa.eu/property/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 

SELECT DISTINCT
bif:concat(xsd:string(?ReportingYear), '-', ?measuresXMLURI, '-', ?SpatialScale, '-', ?TimeScale, '-', ?envelope, '-', ?exceedanceAffectedURI,'-',?usedForScenarioURI ) as ?_id
?Country as ?CountryOrTerritory  
?ReportingYear 
?Namespace 
?MeasureId 
?VersionId 
?CodeOfMeasure 
?Name 
?Description 
?MeasureClassification 
?MeasureType 
?AdministrativeLevel 
?TimeScale 
?SpatialScale 
?SourceSectors 
?EstimatedImplementationCosts 
?Currency 
?CommentOnCosts 
?Status 
?ImplementationBegin 
?ImplementationEnd 
?PlannedFullEffectDate 
?MonitoringProgressIndicators 
?QuantityOfReducedEmissions 
?CommentOnEmissionReductions 
?LevelOfConcentration 
?NumberOfExceedances 
?SpecificationOfHours 
?CommentOnExpectedImpact 
?exceedanceAffectedURI as ?SourceApportionmentId 
?usedForScenarioURI as ?ScenariosId 
?envelope

 WHERE {

{SELECT 
?measuresXMLURI 
(IRI(substr(str(?measuresXMLURI),1,49)) AS ?envelope)
 
WHERE {
?measuresXMLURI a aq:AQD_Measures .
}
}

?envelope rod:released ?released .

{
SELECT DISTINCT 
?Country 
?obligation 
(YEAR(xsd:date(?startOfPeriod)) AS ?ReportingYear) 
(max(?released) AS ?released) 

WHERE {
?envelope rod:released ?released ;
          rod:startOfPeriod ?startOfPeriod ;
          rod:obligation <http://rod.eionet.europa.eu/obligations/683> ; 
          rod:obligation ?obligation ;
          rod:locality ?locality .

?locality rod:localityName ?Country .

}
}


?measuresXMLURI a aq:AQD_Measures ;
                     aq:inspireId ?inspireURI ;
                     aq:code ?CodeOfMeasure ;
                     aq:name ?Name ;
                     aq:description ?Description ;
                     aq:classification ?classificationURI ;
                     aq:measureType ?measureTypeURI ;
                     aq:administrativeLevel ?administrativeLevelURI ;
                     aq:timeScale ?timeScaleURI ;
                     aq:spatialScale ?spatialScaleURI ;
                     aq:sourceSectors ?sourceSectorsURI ;
                     aq:costs ?costsURI ;
                     aq:plannedImplementation ?plannedImplementationURI ;
                     aq:reductionOfEmissions ?reductionOfEmissionsURI .

OPTIONAL{?measuresXMLURI aq:expectedImpact ?expectedImpactURI} .
OPTIONAL{?measuresXMLURI aq:exceedanceAffected ?exceedanceAffectedURI} .
OPTIONAL{?measuresXMLURI aq:usedForScenario ?usedForScenarioURI} .

?classificationURI rdfs:label ?MeasureClassification .
?measureTypeURI rdfs:label ?MeasureType .
?administrativeLevelURI rdfs:label ?AdministrativeLevel .
?timeScaleURI rdfs:label ?TimeScale .
?spatialScaleURI rdfs:label ?SpatialScale .
?sourceSectorsURI rdfs:label ?SourceSectors .

OPTIONAL{?costsURI aq:comment ?CommentOnCosts} .
OPTIONAL{?costsURI aq:estimatedImplementationCosts ?estimatedImplementationCosts} .
OPTIONAL{?costsURI aq:currency ?currencyURI} .
OPTIONAL{?currencyURI rdfs:label ?Currency} .

OPTIONAL{?plannedImplementationURI aq:status ?statusURI} .
OPTIONAL{?plannedImplementationURI aq:implementationPlannedTimePeriod ?implementationPlannedTimePeriodURI} .
OPTIONAL{?plannedImplementationURI aq:plannedFullEffectDate ?plannedFullEffectDateURI} .
OPTIONAL{?plannedImplementationURI aq:comment ?CommentOnImplementation} .
OPTIONAL{?plannedImplementationURI aq:monitoringProgressIndicators ?MonitoringProgressIndicators} .

OPTIONAL{?statusURI rdfs:label ?Status} .
OPTIONAL{?plannedFullEffectDateURI rdfs:label ?PlannedFullEffectDate} .
OPTIONAL{?implementationPlannedTimePeriodURI aq:beginPosition ?ImplementationBegin} .
OPTIONAL{?implementationPlannedTimePeriodURI aq:endPosition ?ImplementationEnd} .

OPTIONAL{?reductionOfEmissionsURI aq:quantity ?QuantityOfReducedEmissions} .
OPTIONAL{?reductionOfEmissionsURI aq:comment ?CommentOnEmissionReductions} .
#OPTIONAL{?reductionOfEmissionsURI aq:unit ?unitURI} .

OPTIONAL{?expectedImpactURI aq:levelOfConcentration ?LevelOfConcentration} .
OPTIONAL{?expectedImpactURI aq:numberOfExceedances ?NumberOfExceedances} .
OPTIONAL{?expectedImpactURI aq:specificationOfHours ?SpecificationOfHours} .
OPTIONAL{?expectedImpactURI aq:comment ?CommentOnExpectedImpact} .

?inspireURI aq:namespace ?Namespace ;
            aq:localId ?MeasureId ;
            aq:versionId ?VersionId .
  
}