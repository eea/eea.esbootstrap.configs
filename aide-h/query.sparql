PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX aqr: <http://reference.eionet.europa.eu/aq/ontology/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX aqdd: <http://dd.eionet.europa.eu/property/>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#> 

SELECT DISTINCT

md5(concat(?Country, xsd:string(?ReportingYear),?PlanCode ,?Namespace ,?PlanId ,?VersionId ,?Name, xsd:string(?FirstExceedanceYear) ,xsd:string(?AdoptionDate) ,?Status ,xsd:string(?TimeTable) ,?ReferenceAQPlan ,?ReferenceImplementation,?Pollutant ,?ProtectionTarget ,?PublicationTitle ,xsd:string(?PublicationDate) ,?PublicationAuthor,?PublicationDescription ,?Publisher ,?PublicationWeblink ,?Organisation ,?IndividualName,?Email ,xsd:string(?Phone) ,?Website ,?Address ,?Comment ,?AttainmentId ,?envelope
)
)  as ?_id

#?planXMLURI
#bif:concat(xsd:string(YEAR(?endOfPeriod)), '-', ?PlanId ) as ?_id
?Country as ?CountryOrTerritory 
?ReportingYear 
?PlanCode 
?Namespace 
?PlanId 
?VersionId 
?Name 
?FirstExceedanceYear 
?AdoptionDate 
?Status 
?TimeTable 
?ReferenceAQPlan 
?ReferenceImplementation 
?Pollutant 
?ProtectionTarget 
?PublicationTitle 
?PublicationDate 
?PublicationAuthor 
?PublicationDescription 
?Publisher 
?PublicationWeblink 
?Organisation 
?IndividualName 
?Email 
?Phone 
?Website 
?Address 
?Comment 
?AttainmentId 
?envelope

 WHERE {

{
SELECT DISTINCT 
?Country 
(YEAR(xsd:date(?endOfPeriod)) AS ?ReportingYear)
?PlanId 
(max(?released) AS ?released) 

WHERE {
?envelope rod:released ?released ;
          rod:endOfPeriod ?endOfPeriod ;
          rod:obligation <http://rod.eionet.europa.eu/obligations/680> ; 
          rod:hasFile ?source ;
          rod:locality ?locality .

?locality rod:localityName ?Country .

{SELECT 
?planXMLURI
?PlanId 
(IRI(substr(str(?planXMLURI),1,bif:strchr(str(?planXMLURI),"#"))) AS ?source) 

WHERE {
?planXMLURI a aq:AQD_Plan ;
              aq:inspireId ?inspireURI .
?inspireURI aq:localId ?PlanId .
}
}
}
}

{
SELECT DISTINCT 
?Country 
(YEAR(xsd:date(?endOfPeriod)) AS ?ReportingYear)
?planXMLURI
?PlanId 
?released
?envelope

WHERE {
?envelope rod:released ?released ;
          rod:endOfPeriod ?endOfPeriod ;
          rod:obligation <http://rod.eionet.europa.eu/obligations/680> ; 
          rod:hasFile ?source ;
          rod:locality ?locality .

?locality rod:localityName ?Country .

{SELECT 
?planXMLURI
?PlanId 
(IRI(substr(str(?planXMLURI),1,bif:strchr(str(?planXMLURI),"#"))) AS ?source) 

WHERE {
?planXMLURI a aq:AQD_Plan ;
              aq:inspireId ?inspireURI .
?inspireURI aq:localId ?PlanId .
}
}
}
}


   ?planXMLURI a aq:AQD_Plan ;
                 aq:inspireId ?inspireURI ;
                 aq:code ?PlanCode ;
                 aq:name ?Name ;                 
                 aq:competentAuthority ?compAuthorityURI ;
                 aq:firstExceedanceYear ?firstexcURI ;
                 aq:adoptionDate ?adoptionURI ;
                 aq:timeTable ?TimeTable ;
                 aq:referenceAQPlan ?ReferenceAQPlan ;
                 aq:referenceImplementation ?ReferenceImplementation ;
                 aq:pollutants ?pollURI ;
                 aq:publication ?publURI ;
                 aq:status ?StatusURI .

OPTIONAL{?planXMLURI aq:comment ?Comment} . 
OPTIONAL{?planXMLURI aq:exceedanceSituation ?excURI} .
OPTIONAL{?excURI aqr:inspireId ?AttainmentId} .
OPTIONAL{?excURI dcterms:source ?source} .
OPTIONAL{?excenvelope rod:hasFile ?source} .

?inspireURI aq:namespace ?Namespace ;
            aq:localId ?PlanId .
OPTIONAL{?inspireURI aq:versionId ?VersionId} .

?compAuthorityURI aq:organisationName ?Organisation ;
                  aq:individualName ?IndividualName ;
                  aq:contact ?contactURI .

?contactURI aq:electronicMailAddress ?Email ;
            aq:telephoneVoice ?Phone ;
            aq:website ?Website ;
            aq:address ?addressURI .

?addressURI aq:locatorDesignator ?Address .
?firstexcURI aq:timePosition ?FirstExceedanceYear .
?adoptionURI aq:timePosition ?AdoptionDate .

?pollURI aq:pollutantCode ?pollcodeURI ;
         aq:protectionTarget ?proTargetURI .

?pollcodeURI skos:notation ?Pollutant .
?proTargetURI rdfs:label ?ProtectionTarget .

?publURI aq:title ?PublicationTitle ;
         aq:description ?PublicationDescription ;
         aq:publisher ?Publisher ;
         aq:webLink ?PublicationWeblink ;
         aq:publicationDate ?DateURI .

OPTIONAL{?publURI aq:author ?PublicationAuthor}.

?DateURI aq:timePosition ?PublicationDate . 
?StatusURI rdfs:label ?Status .

#FILTER regex(?Country, "Poland") .
#FILTER regex(?Country, "United Kingdom") .
     
}
