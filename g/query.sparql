PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX aqr: <http://reference.eionet.europa.eu/aq/ontology/>
PREFIX aqdd: <http://dd.eionet.europa.eu/property/>

SELECT DISTINCT  
bif:concat(xsd:string(YEAR(?reportingBegin)), '-', ?AttainmentId ) as ?_id
?CountryOrTerritory 
YEAR(?reportingBegin) as ?ReportingYear 
?Namespace 
?AttainmentId 
?AssessmentId 
?ZoneId 
?ResidentPopulation 
?Pollutant 
?ObjectiveType 
?ReportingMetric 
?ProtectionTarget 
(bif:either(?Exceedance > 0,"TRUE","FALSE") as ?ExceedanceFinal) 
?ExceedanceThreshold 
?AggregationType 
?FinalNumericalExceedance 
?FinalNumberExceedances 
?ClassificationArea
?SurfaceAreaFinal
?RoadLengthFinal
?PopulationExposedFinal
?SensitivePopulationFinal
?EcosystemAreaExposedFinal
?InfrastructureServicesFinal
?ReferenceYearFinal
#?ExceedanceDurationBegin 
#?ExceedanceDurationEnd
?BaseNumericalExceedance 
?BaseNumberExceedances 
?Adjustment 
?AdjustmentSource 
?AdjustmentType 
?AssessmentType 
?AssessmentTypeDescription 
?ModellingAdjustment 
?SamplingAdjustment 
?Comment 
?envelope


 WHERE {

 ?attURI a aqr:Attainment ;
                dcterms:source ?source;
                aqr:inspireId ?AttainmentId ;
                aqr:inspireNamespace ?Namespace ;
                aqr:environmentalObjective ?envURI ;
                aqr:assessment ?areURI ;
                aqr:zone ?ZoneURI ;
                aqr:pollutant ?PollutantURI .

?envelope rod:hasFile ?source .
?envelope rod:locality ?locURI .
?locURI rdfs:label ?CountryOrTerritory .
         
OPTIONAL{?areURI aqr:inspireId ?AssessmentId} .
?PollutantURI skos:notation ?Pollutant .

OPTIONAL{?attURI aqr:reportingBegin ?reportingBegin} .
OPTIONAL{?attURI aqr:comment ?Comment} .
OPTIONAL{?attURI aqr:exceedanceFinal ?Exceedance} .
OPTIONAL{?attURI aqr:assessmentTypeDescription ?AssessmentTypeDescription} .
OPTIONAL{?attURI aqr:assessmentType ?asstypeURI} .
OPTIONAL{?asstypeURI rdfs:label ?AssessmentType} .
OPTIONAL{?attURI aqr:adjustmentSource ?sourceAdjURI} .
OPTIONAL{?sourceAdjURI rdfs:label ?AdjustmentSource} .
OPTIONAL{?attURI aqr:adjustmentType ?excAdjURI} .
OPTIONAL{?excAdjURI rdfs:label ?AdjustmentType} .
OPTIONAL{?attURI aqr:finalDeductionMethod ?corrURI} .        
OPTIONAL{?corrURI rdfs:label ?Adjustment} .

OPTIONAL{?attURI aqr:modelAssessmentMetadata ?ModellingAdjustment} .
OPTIONAL{?attURI aqr:samplingpointAssessmentMetadata ?SamplingAdjustment} .

OPTIONAL{?envURI aqr:reportingMetric ?ReportingMetricURI} .
OPTIONAL{?envURI aqr:objectiveType ?ObjectiveTypeURI} .
OPTIONAL{?envURI aqr:protectionTarget ?ProtectionTargetURI} .

OPTIONAL{?ReportingMetricURI rdfs:label ?ReportingMetric} .
OPTIONAL{?ObjectiveTypeURI rdfs:label ?ObjectiveType} .
OPTIONAL{?ProtectionTargetURI rdfs:label ?ProtectionTarget} .

OPTIONAL{?ZoneURI aqr:inspireId ?ZoneId} .
OPTIONAL{?ZoneURI aqr:residentPopulation ?ResidentPopulation} .
 
OPTIONAL{?attURI aqr:numericalExceedanceFinal ?FinalNumericalExceedance} .
OPTIONAL{?attURI aqr:numberExceedancesFinal ?FinalNumberExceedances} .
OPTIONAL{?attURI aqr:numericalExceedanceBase ?BaseNumericalExceedance} .
OPTIONAL{?attURI aqr:numberExceedancesBase ?BaseNumberExceedances} .

OPTIONAL{?AggregationTypeURI aqdd:broaderMetric ?ReportingMetricURI} . 

?AggregationTypeURI rdfs:label ?AggregationType .

?thresURI aqdd:relatedPollutant ?PollutantURI .
?thresURI aqdd:aggregationProcess ?AggregationTypeURI .
?thresURI aqdd:hasReportingMetric ?ReportingMetricURI .
?thresURI aqdd:hasObjectiveType ?ObjectiveTypeURI .
?thresURI aqdd:hasProtectionTarget ?ProtectionTargetURI .
?thresURI aqdd:exceedanceThreshold ?ExceedanceThreshold .
?thresURI skos:notation ?NrId .

OPTIONAL{?attURI aqr:areaExceedancefinal ?areaFinalURI} .
OPTIONAL{?attURI aqr:exposureExceedanceFinal ?exposureFinalURI} .

OPTIONAL{?areaFinalURI aqr:areaExceedanceFinal ?ClassificationArea} .
OPTIONAL{?areaFinalURI aqr:surfaceArea ?SurfaceAreaFinal} .
OPTIONAL{?areaFinalURI aqr:roadLength ?RoadLengthFinal} .

OPTIONAL{?exposureFinalURI aqr:populationExposed ?PopulationExposedFinal} .
OPTIONAL{?exposureFinalURI aqr:ecosystemAreaExposed ?EcosystemAreaExposedFinal} .
OPTIONAL{?exposureFinalURI aqr:referenceYear ?ReferenceYearFinal} .
OPTIONAL{?exposureFinalURI aqr:sensitivePopulation ?SensitivePopulationFinal} .
OPTIONAL{?exposureFinalURI aqr:infrastructureServices ?InfrastructureServicesFinal} .

FILTER (xsd:int(?NrId) != 85) .
#FILTER regex(?Namespace,"ES.BDCA.AQ") .
#FILTER regex(?PollutantURI, "") .
            
} ORDER BY ?CountryOrTerritory ASC(?reportingBegin) DESC(?ExceedanceFinal) ASC(?PollutantURI) DESC(?ReportingMetric) DESC(xsd:int(?NumericalExceedance)) DESC(xsd:int(?NumberExceedances))