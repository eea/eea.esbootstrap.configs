
PREFIX aq: <http://rdfdata.eionet.europa.eu/airquality/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX rod: <http://rod.eionet.europa.eu/schema.rdf#>
PREFIX ddaq: <http://dd.eionet.europa.eu/vocabulary/aq/#>

SELECT DISTINCT

md5(concat(?CountryOrTerritory,xsd:string(?startOfPeriod),?Namespace,?Network,?TimeZone,?responsibleParty,?StationName,?EoICode,?StationType,?StationArea,?Pollutant,?SamplingPointId,xsd:string(?Longitude),xsd:string(?Latitude),?MeasurementRegime,xsd:string(?beginPosition),xsd:string(?endPosition),?Sample,?SamplingProcess,?MeasurementEquipment,?MeasurementMethod,?SamplingMethod,?AnalyticalTechnique)) as ?_id

?CountryOrTerritory
YEAR(xsd:date(?startOfPeriod)) AS ?ReportingYear
?Namespace
?Network
?TimeZone
?responsibleParty AS ?ResponsibleParty
?StationName
?EoICode
?StationType
?StationArea
?Pollutant
?SamplingPointId
?Longitude
?Latitude
?Altitude
?MeasurementRegime
?beginPosition AS ?OperationalActivityBegin
?endPosition AS ?OperationalActivityEnd
?Sample
?SamplingProcess
?MeasurementEquipment
?MeasurementMethod
?SamplingMethod
?AnalyticalTechnique
?envelope
#?netURI
#?staURI
#?samplPointURI
#?sampleURI
#?processURI

WHERE {{
  #test data
  #values ?envelope { <http://cdr.eionet.europa.eu/de/eu/aqd/d/colxqbmxw/envxqbmcg> }
  #values ?released { '2020-04-30T09:40:37Z' }
  #values ?startOfPeriod { '2019-01-01' }
  #values ?CountryOrTerritory { 'Germany' }
  #GRAPH <http://cdr.eionet.europa.eu/de/eu/aqd/d/colxqbmxw/envxqbmcg/DE_D_allInOne_metaMeasurements_2020.xml> {

  values ?envelope { <<envelope>> }
  values ?released { '<released>' }
  values ?startOfPeriod { '<startOfPeriod>' }
  values ?CountryOrTerritory { '<CountryOrTerritory>' }
  GRAPH <<source>> {
    ?netURI a aq:AQD_Network .
    ?netURI aq:aggregationTimeZone ?TimeZoneURI .
    ?netURI aq:name ?Network .
    ?netURI aq:responsibleParty ?ResponsibleURI .
    ?netURI aq:declarationFor ?net .

    OPTIONAL{?ResponsibleURI aq:organisationName ?responsibleParty} .

    ?staURI a aq:AQD_Station .
    ?staURI aq:name ?StationName . 
    ?staURI aq:EUStationCode ?EoICode .
    ?staURI aq:areaClassification ?areaClassification .
    ?staURI aq:altitude ?Altitude .
    ?staURI aq:declarationFor ?sta .

    ?samplPointURI a aq:AQD_SamplingPoint .
    ?samplPointURI aq:inspireId ?inspireSPO .
    ?samplPointURI aq:observingCapability ?observingCapability .
    ?samplPointURI aq:relevantEmissions ?relevantEmissions .
    ?samplPointURI aq:measurementRegime ?measurementRegimeURI .
    ?samplPointURI aq:belongsTo ?net .
    ?samplPointURI aq:broader ?sta .

    ?inspireSPO aq:localId ?SamplingPointId .
    ?inspireSPO aq:namespace ?Namespace .

    ?relevantEmissions aq:stationClassification ?stationClassification .

    OPTIONAL{
        ?samplPointURI geo:lat ?Latitude .
        ?samplPointURI geo:long ?Longitude .
    } 

    ?observingCapability aq:featureOfInterest ?sample .
    ?observingCapability aq:procedure ?process .
    ?observingCapability aq:observedProperty ?observedProperty .
    ?observingCapability aq:observingTime ?observingTime .
    ?observingTime aq:beginPosition ?beginPosition .
    OPTIONAL{?observingTime aq:endPosition ?endPosition} .

    ?sampleURI a aq:AQD_Sample .
    ?sampleURI aq:inspireId ?inspireSAM .
    ?sampleURI aq:declarationFor ?sample .

    ?inspireSAM aq:localId ?Sample .

    ?processURI a aq:AQD_SamplingPointProcess .
    ?processURI aq:inspireId ?inspireSPP .
    ?processURI aq:declarationFor ?process .

    ?inspireSPP aq:localId ?SamplingProcess .

 }

 OPTIONAL{ ?processURI aq:measurementEquipment ?measurementEquipment .
           ?measurementEquipment aq:equipment ?equipment .
           ?equipment rdfs:label ?MeasurementEquipment .
 }
  
 OPTIONAL{ ?processURI aq:measurementMethod ?measurementMethod1 .
           ?measurementMethod1 aq:measurementMethod ?measurementMethod2 .
           ?measurementMethod2 rdfs:label ?MeasurementMethod .
 }

 OPTIONAL{ ?processURI aq:samplingMethod ?samplingMethod1 .
           ?samplingMethod1 aq:samplingMethod ?samplingMethod2 .
           ?samplingMethod2 rdfs:label ?SamplingMethod .
 }

 OPTIONAL{ ?processURI aq:analyticalTechnique ?analyticalTechnique1 .
           ?analyticalTechnique1 aq:analyticalTechnique ?analyticalTechnique2 .
           ?analyticalTechnique2 rdfs:label ?AnalyticalTechnique .
 }
 OPTIONAL{ ?TimeZoneURI skos:notation ?TimeZone } .

 OPTIONAL{ ?relevantEmissions aq:stationClassification ?stationClassification .         
           ?stationClassification skos:prefLabel ?StationType .
 }
 OPTIONAL{ ?areaClassification skos:prefLabel ?StationArea } .
 OPTIONAL{ ?observedProperty rdfs:label ?Pollutant } .
 OPTIONAL{ ?measurementRegimeURI rdfs:label ?MeasurementRegime } .
 OPTIONAL{ ?ResponsibleURI aq:organisationName ?responsibleParty } .
}}


